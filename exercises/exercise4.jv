transform CelsiusToFahrenheit {
    from Celsius oftype decimal;
    to Fahrenheit oftype decimal;

    Fahrenheit: (Celsius * 9/5) + 32 ;
}

valuetype PositiveInt oftype integer {
    constraints: [ PositiveInt ];
}

constraint PositiveInt oftype RangeConstraint {
    lowerBound: 0;
}

pipeline Temperatures {

  DataExtractor
    -> ZipInterpreter
    -> TempsFilePicker
    -> TempsTextFileInterpreter
    -> TempsCSVInterpreter
    -> ColumnDataSelector
    -> HeaderWriterT
    -> HeaderWriterB
    -> BatteryTableInterpreter
    -> TemperaturConverterT
    -> TemperaturConverterB
    -> SqlLoader;

  block DataExtractor oftype HttpExtractor {
    url: "https://www.mowesta.com/data/measure/mowesta-dataset-20221107.zip";
  }

  block ZipInterpreter oftype ArchiveInterpreter {
    archiveType: "zip";
  }

  block TempsFilePicker oftype FilePicker {
    path: "/data.csv";
  }

  block TempsTextFileInterpreter oftype TextFileInterpreter {
  }
  
  block TempsCSVInterpreter oftype CSVInterpreter { 
    delimiter: ";";
  }

  block ColumnDataSelector oftype CellRangeSelector {
    select: range A1:K*;
  }

  block HeaderWriterT oftype CellWriter {
    at: cell E1;
    write: ["Temperatur"];
  }

  block HeaderWriterB oftype CellWriter {
    at: cell J1;
    write: ["Batterietemperatur"];
  }

  block BatteryTableInterpreter oftype TableInterpreter {
    header: true;
    columns: [
      "Geraet" oftype PositiveInt,
      "Hersteller" oftype text,
      "Model" oftype text,
      "Monat" oftype integer,
      "Temperatur" oftype decimal,
      "Batterietemperatur" oftype decimal,
      "Geraet aktiv" oftype text
    ];
  }

  block TemperaturConverterT oftype TableTransformer {
    inputColumn: "Temperatur";
    outputColumn: "Temperatur";
    use: CelsiusToFahrenheit;
  }

  block TemperaturConverterB oftype TableTransformer {
    inputColumn: "Batterietemperatur";
    outputColumn: "Batterietemperatur";
    use: CelsiusToFahrenheit;
  }

  block SqlLoader oftype SQLiteLoader { 
    table: "temperatures"; 
    file: "temperatures.sqlite"; 
  }
}
